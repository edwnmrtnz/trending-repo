// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    alias(libs.plugins.android.app).apply(false)
    alias(libs.plugins.kotlin.android).apply(false)
    alias(libs.plugins.android.lib).apply(false)
    alias(libs.plugins.ktlint).apply(true)
    alias(libs.plugins.detekt).apply(true)
}

apply from: "buildscripts/githooks.gradle"
subprojects {
    apply from: "../buildscripts/ktlint.gradle"
    apply from: "../buildscripts/detekt.gradle"

    afterEvaluate { project ->
        if(!project.hasProperty("android")) return
        android {
            compileSdk 33
            defaultConfig {
                minSdk 21
                targetSdk 33
            }

            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
                }
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }

            kotlinOptions {
                jvmTarget = '17'
            }

            buildFeatures {
                viewBinding = true
                buildConfig = true
            }
        }
    }
}

tasks.getByPath("app:preBuild").dependsOn installHooks
