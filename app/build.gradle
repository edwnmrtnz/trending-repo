plugins {
    alias(libs.plugins.android.app)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.dagger.hilt)
    alias(libs.plugins.kotlin.kapt)
}
def KEYSTORE = rootProject.file("app/keystore.properties")

android {
    signingConfigs {
        release {
            if (!KEYSTORE.exists()) return
            def properties = new Properties()
            properties.load(new FileInputStream(KEYSTORE))
            storeFile rootProject.file("app/${properties['STORE_FILE']}")
            storePassword properties['STORE_PASSWORD']
            keyAlias properties['KEY_ALIAS']
            keyPassword properties['KEY_PASSWORD']
        }
    }

    namespace 'com.edwnmrtnz.trendingrepo'
    defaultConfig {
        applicationId "com.edwnmrtnz.trendingrepo"
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation(libs.bundles.app)
    kapt(libs.bundles.kapt)

    testImplementation(libs.bundles.testing.local)
    androidTestImplementation(libs.bundles.testing.instrumented)

    implementation project(path: ':presenter')
}

kapt {
    correctErrorTypes true
}